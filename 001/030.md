Long Pine Cormorant

high

# A malicious attacker can brick the pair permanently and make `takeOverPool` function useless.

## Summary
If a malicious actor creates a pair and set values in `initParams` too high, it will make the pair dysfunctional permanently, and [`takeOverPool`](https://github.com/sherlock-audit/2024-03-goat-trading/blob/main/goat-trading/contracts/exchange/GoatV1Pair.sol#L452) won't be able to reclaim access of the pool.

## Vulnerability Detail
The pair contract has a few key functions which modifies states: `mint`, `swap`, `burn`, `takeOverPool`, `withdrawExcessToken`, and `withdrawFees`. Among them, `takeOverPool` is meant to reclaim access of the pool from malicious attackers which may frontrun to create the pair, and make the pair function again. However, this function can be blocked if certain values in `initParams` is set to really high values.

In `GoatV1Factoty`, anyone can create the pair, and it takes two parameters, the second one being `initParams` which sets initial parameters for the pair.

```solidity
    function createPair(address token, GoatTypes.InitParams memory params) external returns (address) {
        // @note is there a need to have minimum values for theser params so it can't be frontrun?
        if (params.bootstrapEth == 0 || params.virtualEth == 0 || params.initialTokenMatch == 0) {
            revert GoatErrors.InvalidParams();
        }
        if (pools[token] != address(0)) {
            revert GoatErrors.PairExists();
        }
        if (token == weth) {
            revert GoatErrors.CannnotPairWithBaseAsset();
        }
        GoatV1Pair pair = new GoatV1Pair();
        pair.initialize(token, weth, baseName, params);
        pools[token] = address(pair);
        emit PairCreated(token, weth, address(pair));
        return address(pair);
    }
```

The cost of creating such pair is essentially nothing, this is alright since there is the `takeOverPool` function in the pair, the dev team can always take over the pool to make sure the pool works again. Below is a snippet of the function:

```solidity
        localVars.virtualEthOld = _virtualEth;
        localVars.bootstrapEthOld = _bootstrapEth;
        localVars.initialTokenMatchOld = _initialTokenMatch;

        (localVars.tokenAmountForPresaleOld, localVars.tokenAmountForAmmOld) = _tokenAmountsForLiquidityBootstrap(  // <=(1)
            localVars.virtualEthOld,
            localVars.bootstrapEthOld,
            initialLpInfo.initialWethAdded,
            localVars.initialTokenMatchOld
        );

        // new token amount for bootstrap if no swaps would have occured
        (localVars.tokenAmountForPresaleNew, localVars.tokenAmountForAmmNew) = _tokenAmountsForLiquidityBootstrap(  // <=(2)
            initParams.virtualEth, initParams.bootstrapEth, initParams.initialEth, initParams.initialTokenMatch
        );

        // team needs to add min 10% more tokens than the initial lp to take over
        localVars.minTokenNeeded =
            ((localVars.tokenAmountForPresaleOld + localVars.tokenAmountForAmmOld) * 11000) / 10000;

        if ((localVars.tokenAmountForAmmNew + localVars.tokenAmountForPresaleNew) < localVars.minTokenNeeded) {  // <=(3)
            revert GoatErrors.InsufficientTakeoverTokenAmount();
        }

```

We see in (1), old value of presale and AMM token amount is calculated, and in (2), new values are also calculated. Note values in (1) are malicious, while the ones in (2) are trusted and valid values. Last, in (3), the total values in (2) is compares with the total in (1). And the calculation logic is:

```solidity
    function _tokenAmountsForLiquidityBootstrap(
        uint256 virtualEth,
        uint256 bootstrapEth,
        uint256 initialEth,
        uint256 initialTokenMatch
    ) internal pure returns (uint256 tokenAmtForPresale, uint256 tokenAmtForAmm) {
        uint256 k = virtualEth * initialTokenMatch;
        tokenAmtForPresale = initialTokenMatch - (k / (virtualEth + bootstrapEth));
        uint256 totalEth = virtualEth + bootstrapEth;
        tokenAmtForAmm = (k * bootstrapEth) / (totalEth * totalEth);

        if (initialEth != 0) {
            uint256 numerator = (initialEth * initialTokenMatch);
            uint256 denominator = virtualEth + initialEth;
            uint256 tokenAmountOut = numerator / denominator;
            tokenAmtForPresale -= tokenAmountOut;
        }
    }
```

If `initialTokenMatch` and `bootstrapEth` are set to a ridiculously high value, for example, `type(uint112).max`, and `virtualEth` to a low value, this will make `tokenAmtForPresale` an astronomical value, as run in the test here:

```solidity
        GoatTypes.InitParams memory initParams;
        initParams.virtualEth = 1;
        initParams.initialEth = 0;
        initParams.initialTokenMatch = type(uint112).max;
        initParams.bootstrapEth = type(uint112).max;

        (uint256 tokenAmtForPresale, uint256 tokenAmtForAmm) = _tokenAmountsForLiquidityBootstrap(
            initParams.virtualEth, 
            initParams.bootstrapEth, 
            0, 
            initParams.initialTokenMatch);

```

This makes `tokenAmtForPresale` to be `5192296858534827628530496329220095`, which is around 1e33 tokens required. Now, back to (3), and this will revert as the value will be too high for normal operation values. As a result, other functions are also completely dysfunctional.

Such as `mint`:

```solidity
        mintVars.virtualEth = _virtualEth;
        mintVars.initialTokenMatch = _initialTokenMatch;
        mintVars.bootstrapEth = _bootstrapEth;

        if (_vestingUntil == _MAX_UINT32) {
            // Do not allow to add liquidity in presale period
            if (totalSupply_ > 0) revert GoatErrors.PresalePeriod();
            // don't allow to send more eth than bootstrap eth
            if (balanceEth > mintVars.bootstrapEth) {
                revert GoatErrors.SupplyMoreThanBootstrapEth();
            }

            if (balanceEth < mintVars.bootstrapEth) {
                (uint256 tokenAmtForPresale, uint256 tokenAmtForAmm) = _tokenAmountsForLiquidityBootstrap( // <=(4)
                    mintVars.virtualEth, mintVars.bootstrapEth, balanceEth, mintVars.initialTokenMatch
                );
                if (balanceToken != (tokenAmtForPresale + tokenAmtForAmm)) {  // <=(5)
                    revert GoatErrors.InsufficientTokenAmount();
                }
                liquidity =
                    Math.sqrt(uint256(mintVars.virtualEth) * uint256(mintVars.initialTokenMatch)) - MINIMUM_LIQUIDITY;
            } else {
                // This means that user is willing to make this pool an amm pool in first liquidity mint
                liquidity = Math.sqrt(balanceEth * balanceToken) - MINIMUM_LIQUIDITY;
                uint32 timestamp = uint32(block.timestamp);
                _vestingUntil = timestamp + VESTING_PERIOD;
            }
```

In (4), a similar pair of values are also calculated, and compared with the pool's token balance in (5). This will likely fail and revert again. Or in other case, when `balanceEth == mintVars.bootstrapEth`, which is also unlikely to happen as the value set is too high. 

In conclusion, if a malicious attacker successfully created this pool with this malicious params. There are no ways of recovering it, and since the factory contract sets the pair map on creation, the only way to remove it is through `removePair`, which is only called in `withdrawExcessToken`. This doesn't work because:

```solidity
    function withdrawExcessToken() external {
        uint256 timestamp = block.timestamp;
        // initial liquidty provider can call this function after 30 days from genesis
        if (_genesis + _THIRTY_DAYS > timestamp) revert GoatErrors.PresaleDeadlineActive();
        if (_vestingUntil != _MAX_UINT32) {
            revert GoatErrors.ActionNotAllowed();
        }

        address initialLiquidityProvider = _initialLPInfo.liquidityProvider;
        if (msg.sender != initialLiquidityProvider) {
            revert GoatErrors.Unauthorized();
        }

```

Only LP can call this. And LP is not set in `initialize`, so it will be `address(0)`. This leaves no option to recover the pair, and no other pair can be created as a substitution because how factory works.

## Impact
As explained in the vulnerability detail section, this allows a malicious actor completely DoS the pair, and with no way of recovering it. This is critical because the cost of attack is low, and can block all major tokens which makes the protocol not operational.

## Code Snippet
```solidity
    function createPair(address token, GoatTypes.InitParams memory params) external returns (address) {
        // @note is there a need to have minimum values for theser params so it can't be frontrun?
        if (params.bootstrapEth == 0 || params.virtualEth == 0 || params.initialTokenMatch == 0) {
            revert GoatErrors.InvalidParams();
        }
        if (pools[token] != address(0)) {
            revert GoatErrors.PairExists();
        }
        if (token == weth) {
            revert GoatErrors.CannnotPairWithBaseAsset();
        }
        GoatV1Pair pair = new GoatV1Pair();
        pair.initialize(token, weth, baseName, params);
        pools[token] = address(pair);
        emit PairCreated(token, weth, address(pair));
        return address(pair);
    }
```

## Tool used

Manual Review, foundry

## Recommendation
Consider making whoever creates the pair to be the first LP, and they have to provide assets in order to deploy the pair. Also consider adding an emergency remove function which removes the pair from factory when such cases happen.
