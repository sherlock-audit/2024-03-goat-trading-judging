Sticky Flaxen Rat

high

# Swap Fails when Fee On Transfer tokens is being Swapped to Weth Leading to DOS

## Summary

According to the Q/A, this protocol is supposed to support Fee on Transfer tokens, https://audits.sherlock.xyz/contests/178. The issue here is Fee on Transfer Tokens cannot be swapped. It is not possible to swap from a Fee on Transfer Token to Weth but you can swap from Weth to a Fee on Transfer token.

## Vulnerability Details

The root cause of this Vulnerability lies in the [GoatRouterV1.sol:swapExactTokensForWeth](https://github.com/sherlock-audit/2024-03-goat-trading/blob/main/goat-trading/contracts/periphery/GoatRouterV1.sol#L175) function. `amountWethOut` is calculated with the `amountIn` value the `amountIn` value is then transferred to the `pool`. After which the swap is performed using the `amountWethOut` value.

This works fine for normal tokens but for Fee on transfer Tokens, `amountIn` is not the same as the amount being received by the `pool`, the `pool` receives fewer funds than the funds that were sent. So trying to perform the swap using the `amountWethOut` will make the transaction revert because `amountIn` differs from the amount received.

```solidity
    function swapExactTokensForWeth(uint256 amountIn, uint256 amountOutMin, address token, address to, uint256 deadline)
        public
        ensure(deadline)
        nonReentrant
        returns (uint256 amountWethOut)
    {
        if (amountIn == 0) {
            revert GoatErrors.InsufficientInputAmount();
        }

        GoatTypes.LocalVariables_PairStateInfo memory vars;
        GoatV1Pair pair = GoatV1Pair(GoatV1Factory(FACTORY).getPool(token));
        if (pair == GoatV1Pair(address(0))) {
            revert GoatErrors.GoatPoolDoesNotExist();
        }
        if (pair.vestingUntil() != type(uint32).max) {
            (uint112 reserveEth, uint112 reserveToken) = pair.getStateInfoAmm();
@->         amountWethOut = GoatLibrary.getWethAmountOutAmm(amountIn, reserveEth, reserveToken);
        } else {
            (vars.reserveEth, vars.reserveToken, vars.virtualEth,,, vars.virtualToken) = pair.getStateInfoForPresale();

@->         amountWethOut = GoatLibrary.getWethAmountOutPresale(
                amountIn, vars.reserveEth, vars.reserveToken, vars.virtualEth, vars.virtualToken
            );
        }

        if (amountWethOut < amountOutMin) {
            revert GoatErrors.InsufficientAmountOut();
        }
@->     IERC20(token).safeTransferFrom(msg.sender, address(pair), amountIn);
@->     pair.swap(0, amountWethOut, to);
    }

```

## POC

Create a file in this folder https://github.com/sherlock-audit/2024-03-goat-trading-joshuajee/tree/main/goat-trading/test/foundry

Copy and paste the code snippet below into that file. Use the command to run the test.

```bash
forge test --mt testSwapPOC -vvv
```

<details>
    <summary> Proof of code </summary>
    
```sol

// SPDX-License-Identifier: UNLICENSED
pragma solidity 0.8.19;

import {Test} from "forge-std/Test.sol";
import {console2} from "forge-std/Test.sol";
import {ERC20} from "@openzeppelin/contracts/token/ERC20/ERC20.sol";
import {GoatV1Pair} from "../../contracts/exchange/GoatV1Pair.sol";
import {GoatV1Factory} from "../../contracts/exchange/GoatV1Factory.sol";
import {GoatV1Router} from "../../contracts/periphery/GoatRouterV1.sol";
import {GoatV1ERC20} from "../../contracts/exchange/GoatV1ERC20.sol";
import {MockWETH} from "../../contracts/mock/MockWETH.sol";
import {FeeOnTransferToken} from "../../contracts/mock/FeeOnTransferToken.sol";
import {GoatTypes} from "../../contracts/library/GoatTypes.sol";
import {GoatV1Pair} from "../../contracts/exchange/GoatV1Pair.sol";

contract GoatV1FOTTest is Test {

    GoatV1Pair public pair;
    GoatV1Factory public factory;
    GoatV1Router public router;
    GoatV1ERC20 public goatToken;
    MockWETH public weth;
    FeeOnTransferToken public token;

    //Users
    address public lp_1 = makeAddr("lp_1");
    address public swapper = makeAddr("swapper");

    struct AddLiquidityParams {
        address token;
        uint256 tokenDesired;
        uint256 wethDesired;
        uint256 tokenMin;
        uint256 wethMin;
        address to;
        uint256 deadline;
        GoatTypes.InitParams initParams;
    }

    AddLiquidityParams public addLiqParams;

    function setUp() public {
        vm.warp(300 days);
        weth = new MockWETH();
        token = new FeeOnTransferToken();
        factory = new GoatV1Factory(address(weth));
        router = new GoatV1Router(address(factory), address(weth));

        // Mint tokens
    }

    function addLiquidityParams(bool initial, bool sendInitWeth) public returns (AddLiquidityParams memory) {
        weth.deposit{value: 100e18}();
        if (initial) {
            /* ------------------------------- SET PARAMS ------------------------------- */
            addLiqParams.token = address(token);
            addLiqParams.tokenDesired = 0;
            addLiqParams.wethDesired = 0;
            addLiqParams.tokenMin = 0;
            addLiqParams.wethMin = 0;
            addLiqParams.to = address(this);
            addLiqParams.deadline = block.timestamp + 1000;

            addLiqParams.initParams = GoatTypes.InitParams(10e18, 10e18, sendInitWeth ? 5e18 : 0, 1000e18);
        } else {
            addLiqParams.token = address(token);
            addLiqParams.tokenDesired = 100e18;
            addLiqParams.wethDesired = 1e18;
            addLiqParams.tokenMin = 0;
            addLiqParams.wethMin = 0;
            addLiqParams.to = address(this);
            addLiqParams.deadline = block.timestamp + 1000;

            addLiqParams.initParams = GoatTypes.InitParams(0, 0, 0, 0);
        }
        return addLiqParams;
    }

    function _addLiquidityAndConvertToAmm()
        internal
        returns (uint256 tokenAmtUsed, uint256 wethAmtUsed, uint256 liquidity, uint256 actualTokenAmountToSend)
    {
        addLiquidityParams(true, true);
        addLiqParams.initParams.initialEth = 10e18; // set all weth
        actualTokenAmountToSend = router.getActualBootstrapTokenAmount(
            addLiqParams.initParams.virtualEth,
            addLiqParams.initParams.bootstrapEth,
            addLiqParams.initParams.initialEth,
            addLiqParams.initParams.initialTokenMatch
        );
        token.approve(address(router), actualTokenAmountToSend);
        weth.approve(address(router), addLiqParams.initParams.initialEth);
        (tokenAmtUsed, wethAmtUsed, liquidity) = router.addLiquidity(
            addLiqParams.token,
            addLiqParams.tokenDesired,
            addLiqParams.wethDesired,
            addLiqParams.tokenMin,
            addLiqParams.wethMin,
            addLiqParams.to,
            addLiqParams.deadline,
            addLiqParams.initParams
        );
    }

   
    function _swapWethToToken() internal returns (uint256 amountOut) {
        weth.transfer(swapper, 5e18);
        vm.startPrank(swapper);
        weth.approve(address(router), 5e18);
        amountOut = router.swapWethForExactTokens(
            5e18,
            0, // no slippage protection for now
            address(token),
            swapper,
            block.timestamp
        );
        vm.stopPrank();
    }

    function testSwapPOC () public {
        _addLiquidityAndConvertToAmm();
        GoatV1Pair pair = GoatV1Pair(factory.getPool(address(token)));
        uint256 amountOut = _swapWethToToken();
        uint256 liquidityFeesbefore = pair.getPendingLiquidityFees();
        uint256 protocolFeesBefore = pair.getPendingProtocolFees();
        uint256 feesBefore = pair.getPendingLiquidityFees() + pair.getPendingProtocolFees();

        amountOut =  amountOut / 10;     // since it is a Fee on Transfer Token A
        // Now swap token to weth
        vm.startPrank(swapper);
        token.approve(address(router), amountOut);
        uint256 amountWethOut = router.swapExactTokensForWeth(
            amountOut, // amountIn
            0, // no slippage protection for now
            address(token),
            swapper,
            block.timestamp
        );
        vm.stopPrank();

    }

}


```

</details>

## Impact

Denial of Service, A Pool Fee on Tranfer Tokens can only support swaps, in one direction.

## Code Snippet

https://github.com/sherlock-audit/2024-03-goat-trading/blob/main/goat-trading/contracts/periphery/GoatRouterV1.sol#L175

## Tool used

Manual Review

## Recommendation

Here is a simple Fix

```diff
    function swapExactTokensForWeth(uint256 amountIn, uint256 amountOutMin, address token, address to, uint256 deadline)
        public
        ensure(deadline)
        nonReentrant
        returns (uint256 amountWethOut)
    {
        if (amountIn == 0) {
            revert GoatErrors.InsufficientInputAmount();
        }

        GoatTypes.LocalVariables_PairStateInfo memory vars;
        GoatV1Pair pair = GoatV1Pair(GoatV1Factory(FACTORY).getPool(token));
        if (pair == GoatV1Pair(address(0))) {
            revert GoatErrors.GoatPoolDoesNotExist();
        }

+       uint initialTokenBal = IERC20(token).balanceOf(address(pair));
+       IERC20(token).safeTransferFrom(msg.sender, address(pair), amountIn);
+       amountIn = IERC20(token).balanceOf(address(pair)) - initialTokenBal;

        if (pair.vestingUntil() != type(uint32).max) {
            (uint112 reserveEth, uint112 reserveToken) = pair.getStateInfoAmm();
            amountWethOut = GoatLibrary.getWethAmountOutAmm(amountIn, reserveEth, reserveToken);
        } else {
            (vars.reserveEth, vars.reserveToken, vars.virtualEth,,, vars.virtualToken) = pair.getStateInfoForPresale();

            amountWethOut = GoatLibrary.getWethAmountOutPresale(
                amountIn, vars.reserveEth, vars.reserveToken, vars.virtualEth, vars.virtualToken
            );
        }

        if (amountWethOut < amountOutMin) {
            revert GoatErrors.InsufficientAmountOut();
        }
        pair.swap(0, amountWethOut, to);
    }

```