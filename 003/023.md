Shiny Eggshell Meerkat

medium

# Divide before multiply in `getTokenAmountIn` computes wrong `amountTokenIn`

## Summary
Function `getTokenAmountIn` of `GoatLibrary` performs multiplication on the result of a division which causes loss of precision and funds.

## Vulnerability Detail
```solidity
File: contracts/library/GoatLibrary.sol

/// @audit ******************* Issue Detail *******************
GoatLibrary.getTokenAmountIn(uint256,uint256,uint256,uint256,uint256,uint32) (contracts/library/GoatLibrary.sol#222-246) performs a multiplication on the result of a division:
	- actualWethOut = ((wethAmountOut * 10000) / 9901) (contracts/library/GoatLibrary.sol#231)
	- actualWethOut = actualWethOut * 10000 (contracts/library/GoatLibrary.sol#237)

/// @audit ************** Possible Issue Line(s) **************
	L#231,  L#237,  

/// @audit ****************** Affected Code *******************
 222:     function getTokenAmountIn(
 223:         uint256 wethAmountOut,
 224:         uint256 reserveEth,
 225:         uint256 reserveToken,
 226:         uint256 virtualEth,
 227:         uint256 virtualToken,
 228:         uint32 vestingUntil
 229:     ) internal pure returns (uint256 amountTokenIn) {
 230:         // scale by 10000 to avoid rounding errors
 231:         uint256 actualWethOut = ((wethAmountOut * 10000) / 9901);
 232:         if (wethAmountOut == 0) revert GoatErrors.InsufficientOutputAmount();
 233:         if (actualWethOut > reserveEth) revert GoatErrors.InsufficientLiquidity();
 234:         uint256 numerator;
 235:         uint256 denominator;
 236:         // scale actual weth out by 10000
 237:         actualWethOut = actualWethOut * 10000;
 238:         if (vestingUntil == type(uint32).max) {
 239:             numerator = actualWethOut * (virtualToken + reserveToken);
 240:             denominator = (virtualEth + reserveEth) * 10000 - actualWethOut;
 241:         } else {
 242:             numerator = actualWethOut * reserveToken;
 243:             denominator = reserveEth * 10000 - actualWethOut;
 244:         }
 245:         amountTokenIn = numerator / denominator;
 246:     }
```

## Impact
the `amountTokenIn` value computed by the function `getTokenAmountIn` will be lesser than the actual value.
Following Foundry Fuzzing test identifies the issue

**PoC Code**
```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.13;

contract GoatLibrary {
    
    function getTokenAmountIn(uint256 wethAmountOut,uint256 reserveEth,uint256 reserveToken,uint256 virtualEth,uint256 virtualToken,uint32 vestingUntil) public pure returns (uint256 amountTokenIn) {
        // scale by 10000 to avoid rounding errors
        uint256 actualWethOut = ((wethAmountOut * 10000) / 9901);
        if (wethAmountOut == 0) revert ("GoatErrors.InsufficientOutputAmount()");
        if (actualWethOut > reserveEth) revert ("GoatErrors.InsufficientLiquidity()");
        uint256 numerator;
        uint256 denominator;
        // scale actual weth out by 10000
        actualWethOut = actualWethOut * 10000;
        if (vestingUntil == type(uint32).max) {
            numerator = actualWethOut * (virtualToken + reserveToken);
            denominator = (virtualEth + reserveEth) * 10000 - actualWethOut;
        } else {
            numerator = actualWethOut * reserveToken;
            denominator = reserveEth * 10000 - actualWethOut;
        }
        amountTokenIn = numerator / denominator;
    }

    //Optimized Code - Division before multiplication removed
    function getTokenAmountIn_Optimized(uint256 wethAmountOut,uint256 reserveEth,uint256 reserveToken,uint256 virtualEth,uint256 virtualToken,uint32 vestingUntil) public pure returns (uint256 amountTokenIn) {
        // scale by 10000 to avoid rounding errors
        uint256 actualWethOut = ((wethAmountOut * 10000) / 9901);
        if (wethAmountOut == 0) revert ("GoatErrors.InsufficientOutputAmount()");
        if (actualWethOut > reserveEth) revert ("GoatErrors.InsufficientLiquidity()");
        uint256 numerator;
        uint256 denominator;
        // scale actual weth out by 10000
        actualWethOut = (wethAmountOut * 10000) * 10000 / 9901;         //division before multiplication removed
        if (vestingUntil == type(uint32).max) {
            numerator = actualWethOut * (virtualToken + reserveToken);
            denominator = (virtualEth + reserveEth) * 10000 - actualWethOut;
        } else {
            numerator = actualWethOut * reserveToken;
            denominator = reserveEth * 10000 - actualWethOut;
        }
        amountTokenIn = numerator / denominator;
    }
}
```

**Fuzz Testing Code**

```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.13;

import {Test, console} from "forge-std/Test.sol";
import {GoatLibrary} from "../src/GoatLibrary.sol";
import "forge-std/console.sol";

contract CounterTest is Test {
    GoatLibrary public lib;

    function setUp() public {
        lib = new GoatLibrary();
    }

    /// forge-config: default.fuzz.runs = 1000
    function testFuzz_getTokenAmountIn_Optimized(uint256 wethAmountOut,uint256 reserveEth,uint256 reserveToken,uint256 virtualEth, uint256 virtualToken,uint32 vestingUntil) public view {
        uint256 low = 1 wei;
        uint256 high = 10000 ether;
        vm.assume(low <= wethAmountOut && wethAmountOut < high);
        uint256 actualWethOut = ((wethAmountOut * 10000) / 9901);
        vm.assume(actualWethOut <= reserveEth);
        vm.assume(low <= reserveToken && reserveToken < high);
        vm.assume(low <= virtualEth && virtualEth < high);
        vm.assume(low <= virtualToken && virtualToken < high);
        vm.assume(1 <= vestingUntil && vestingUntil < type(uint32).max);

        uint256 amountTokenIn = lib.getTokenAmountIn(wethAmountOut, reserveEth, reserveToken, virtualEth, virtualToken, vestingUntil);
        uint256 amountTokenIn_Optimize = lib.getTokenAmountIn_Optimized(wethAmountOut, reserveEth, reserveToken, virtualEth, virtualToken, vestingUntil);

        assertEq(amountTokenIn, amountTokenIn_Optimize);
    }
}
```

**Fuzz Test Output**
```console
└─$ forge test --mt getTokenAmountIn
[⠒] Compiling...
No files changed, compilation skipped

Ran 1 test for test/GoatLibrary.t.sol:CounterTest
[FAIL. Reason: assertion failed: 1524 != 1525; 
counterexample: calldata=0x7a27ebd800000000000000000000000000000000000000000000000000000000000005da00000000000000000000000000000000000000000000000000000000000014190000000000000000000000000000000000000000000000000000000000000e4e000000000000000000000000000000000000000000000000000000007d4ae42600000000000000000000000000000000000000000000000000000000000001c200000000000000000000000000000000000000000000000000000000000005dc 
args=[1498, 5145, 3662, 2102060070 [2.102e9], 450, 1500]]
testFuzz_getTokenAmountIn_Optimized(uint256,uint256,uint256,uint256,uint256,uint32) (runs: 0, μ: 0, ~: 0)
Suite result: FAILED. 0 passed; 1 failed; 0 skipped; finished in 3.75ms (3.07ms CPU time)

Ran 1 test suite in 5.78ms (3.75ms CPU time): 0 tests passed, 1 failed, 0 skipped (1 total tests)
```

## Code Snippet
https://github.com/sherlock-audit/2024-03-goat-trading/blob/main/goat-trading/contracts/library/GoatLibrary.sol#L222-L246

## Tool used
Manual Aided Review and Fuzz Testing

## Recommendation
remove division before multiplication
```solidity
        actualWethOut = (wethAmountOut * 10000) * 10000 / 9901;         //division before multiplication removed
```