Shiny Eggshell Meerkat

medium

# Divide before multiply in getWethAmountOutPresale causes loss of funds

## Summary
Function `getWethAmountOutAmm` of `GoatLibrary` performs multiplication on the result of a division which causes loss of precision and funds.

## Vulnerability Detail
```solidity
File: contracts/library/GoatLibrary.sol

/// @audit ******************* Issue Detail *******************
GoatLibrary.getWethAmountOutAmm(uint256,uint256,uint256) (contracts/library/GoatLibrary.sol#123-141) performs a multiplication on the result of a division:
	- actualAmountWethOut = numerator / denominator (contracts/library/GoatLibrary.sol#138)
	- amountWethOut = (actualAmountWethOut * 9901) / 10000 (contracts/library/GoatLibrary.sol#140)

/// @audit ************** Possible Issue Line(s) **************
	L#138,  L#140,  

/// @audit ****************** Affected Code *******************
 123:     function getWethAmountOutAmm(uint256 amountTokenIn, uint256 reserveEth, uint256 reserveToken)
 124:         internal
 125:         pure
 126:         returns (uint256 amountWethOut)
 127:     {
 128:         if (amountTokenIn == 0) revert GoatErrors.InsufficientInputAmount();
 129:         if (reserveEth == 0 || reserveToken == 0) revert GoatErrors.InsufficientLiquidity();
 130: 
 131:         amountTokenIn = amountTokenIn * 10000;
 132:         uint256 numerator;
 133:         uint256 denominator;
 134:         uint256 actualAmountWethOut;
 135:         // amm logic
 136:         numerator = amountTokenIn * reserveEth;
 137:         denominator = reserveToken * 10000 + amountTokenIn;
 138:         actualAmountWethOut = numerator / denominator;
 139:         // 0.99% fee on WETH
 140:         amountWethOut = (actualAmountWethOut * 9901) / 10000;
 141:     }
```
## Impact
the `amountWethOut` value computed by the function `getWethAmountOutAmm` will be lesser than the actual value.
Following Foundry Fuzzing test identifies the issue

**PoC Code**
```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.13;

contract GoatLibrary {
    
    function getWethAmountOutAmm(uint256 amountTokenIn, uint256 reserveEth, uint256 reserveToken) public pure returns (uint256 amountWethOut)
    {
        if (amountTokenIn == 0) revert ("GoatErrors.InsufficientInputAmount()");
        if (reserveEth == 0 || reserveToken == 0) revert ("GoatErrors.InsufficientLiquidity()");

        amountTokenIn = amountTokenIn * 10000;
        uint256 numerator;
        uint256 denominator;
        uint256 actualAmountWethOut;
        // amm logic
        numerator = amountTokenIn * reserveEth;
        denominator = reserveToken * 10000 + amountTokenIn;
        actualAmountWethOut = numerator / denominator;
        // 0.99% fee on WETH
        amountWethOut = (actualAmountWethOut * 9901) / 10000;
    }

    //Optimized Code - Division before multiplication removed
    function getWethAmountOutAmm_Optimized(uint256 amountTokenIn, uint256 reserveEth, uint256 reserveToken) public pure returns (uint256 amountWethOut)
    {
        if (amountTokenIn == 0) revert ("GoatErrors.InsufficientInputAmount()");
        if (reserveEth == 0 || reserveToken == 0) revert ("GoatErrors.InsufficientLiquidity()");

        amountTokenIn = amountTokenIn * 10000;
        uint256 numerator;
        uint256 denominator;
        uint256 actualAmountWethOut;
        // amm logic
        numerator = amountTokenIn * reserveEth;
        denominator = reserveToken * 10000 + amountTokenIn;
        actualAmountWethOut = numerator / denominator;
        // 0.99% fee on WETH
        //amountWethOut = (actualAmountWethOut * 9901) / 10000;
        amountWethOut = (numerator * 9901) / 10000 / denominator;       //division before multlication removed
    }
}
```

**Fuzz Testing Code**

```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.13;

import {Test, console} from "forge-std/Test.sol";
import {GoatLibrary} from "../src/GoatLibrary.sol";
import "forge-std/console.sol";

contract CounterTest is Test {
    GoatLibrary public lib;

    function setUp() public {
        lib = new GoatLibrary();
    }

    /// forge-config: default.fuzz.runs = 1000
    function testFuzz_getWethAmountOutAmm(uint256 amountTokenIn, uint256 reserveEth, uint256 reserveToken) public view {
        uint256 low = 1 wei;
        uint256 high = 10000 ether;
        vm.assume(low <= amountTokenIn && amountTokenIn < high);
        vm.assume(low <= reserveEth && reserveEth < high);
        vm.assume(low <= reserveToken && reserveToken < high);
        
        uint256 amountWethOut = lib.getWethAmountOutAmm(amountTokenIn, reserveEth, reserveToken);
        uint256 amountWethOut_Optimize = lib.getWethAmountOutAmm_Optimized(amountTokenIn, reserveEth, reserveToken);

        assertEq(amountWethOut, amountWethOut_Optimize);
    }
}
```

**Fuzz Test Output**
```console
└─$ forge test --mt getWethAmountOutAmm
[⠒] Compiling...
[⠢] Compiling 1 files with 0.8.24
[⠆] Solc 0.8.24 finished in 1.09s
Compiler run successful!

Ran 1 test for test/GoatLibrary.t.sol:CounterTest
[FAIL. Reason: assertion failed: 686 != 687; 
counterexample: calldata=0xf2de71a0000000000000000000000000000000000000000000000000000000000000120a00000000000000000000000000000000000000000000000000000000000002c00000000000000000000000000000000000000000000000000000000000000043 
args=[4618, 704, 67]] 
testFuzz_getWethAmountOutAmm(uint256,uint256,uint256) (runs: 0, μ: 0, ~: 0)
Suite result: FAILED. 0 passed; 1 failed; 0 skipped; finished in 1.13ms (846.70µs CPU time)
```

## Code Snippet
https://github.com/sherlock-audit/2024-03-goat-trading/blob/main/goat-trading/contracts/library/GoatLibrary.sol#L123-L141

## Tool used
Manual Aided Review and Fuzz Testing

## Recommendation
remove division before multiplication
```solidity
        actualAmountWethOut = numerator / denominator;
        // 0.99% fee on WETH
        //amountWethOut = (actualAmountWethOut * 9901) / 10000;
        amountWethOut = (numerator * 9901) / 10000 / denominator;       //division before multlication removed
```