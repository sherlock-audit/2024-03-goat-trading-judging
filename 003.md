Faint Slate Mole

high

# GoatV1Pair does not work when fee-on-transfer tokens are used, leading to DoS of contract

## Summary
In the README, it is stated that any ERC20 should be able to work with the protocol:
> Any ERC20 tokens should be able to be used.

However fee-on-transfer tokens are not properly accounted for, leading to DoS of the `mint()` function so LPs will not be able to add liquidity to a pair.

## Vulnerability Detail
The mint function implements the following check:
```solidity
(uint256 tokenAmtForPresale, uint256 tokenAmtForAmm) = _tokenAmountsForLiquidityBootstrap(
        mintVars.virtualEth, mintVars.bootstrapEth, balanceEth, mintVars.initialTokenMatch
    );
    
    if (balanceToken != (tokenAmtForPresale + tokenAmtForAmm)) {
        revert GoatErrors.InsufficientTokenAmount();
    }
```
The issue is that `balanceToken` will always be less than `(tokenAmtForPresale + tokenAmtForAmm)` due to the fee on transfer not being accounted for, leading to a revert.

## Impact
Pairs created to work with Fee On Transfer tokens will not be functional, breaking core protocol functionality.

## Code Snippet
https://github.com/sherlock-audit/2024-03-goat-trading/blob/beb09519ad0c0ec0fdf5b96060fe5e4aafd71cff/goat-trading/contracts/exchange/GoatV1Pair.sol#L139-L141

## Proof of Concept
I've included 2 foundry tests: 1 with a normal token, 1 with a fee-on-transfer token. Minting doesn't work with the fee on transfer token.

To run the tests, modify the file `GoatV1Pair.t.sol` with the following changes:

<details> <summary>Extra initialisation</summary>

**Import:**
`import "../../../contracts/mock/FeeOnTransferToken.sol";`

**Storage Variable:**
`    FeeOnTransferToken feeOnTransferToken; `

**In setUp():**
`   feeOnTransferToken = new FeeOnTransferToken();
`

</details>


<details> <summary> Replace `_mintInitialLiquidity()` with the FOT token: </summary>

```solidity
function _mintInitialLiquidity(GoatTypes.InitParams memory initParams, address to)
        private
        returns (uint256 tokenAmtForPresale, uint256 tokenAmtForAmm)
    {
        (tokenAmtForPresale, tokenAmtForAmm) = GoatLibrary.getTokenAmountsForPresaleAndAmm(
            initParams.virtualEth, initParams.bootstrapEth, initParams.initialEth, initParams.initialTokenMatch
        );
        uint256 bootstrapTokenAmt = tokenAmtForPresale + tokenAmtForAmm;
        _fundMe(IERC20(address(feeOnTransferToken)), to, bootstrapTokenAmt);
        vm.startPrank(to);
        address pairAddress = factory.createPair(address(feeOnTransferToken), initParams);
        console.log(feeOnTransferToken.balanceOf(to)); // 7.425e18
        if (bootstrapTokenAmt != 0) {
            feeOnTransferToken.transfer(pairAddress, feeOnTransferToken.balanceOf(to));
        }
        if (initParams.initialEth != 0) {
            vm.deal(to, initParams.initialEth);
            weth.deposit{value: initParams.initialEth}();
            weth.transfer(pairAddress, initParams.initialEth);
        }
        pair = GoatV1Pair(pairAddress);
        
        pair.mint(to);

        vm.stopPrank();
    }
    
```

</details>

<details> 
<summary> Foundry tests </summary>

```solidity
function test_mintingWorksWithNormalToken() public {
        GoatTypes.InitParams memory initParams;
        initParams.virtualEth = 10e18;
        initParams.initialEth = 0;
        initParams.initialTokenMatch = 1000e18;
        initParams.bootstrapEth = 10e18;

        // No fee on transfer
        feeOnTransferToken.setFeePercentage(0);

        _mintInitialLiquidity(initParams, users.lp);

        uint256 initialLpBalance = pair.balanceOf(users.lp);
        assertEq(initialLpBalance, 100e18 - MINIMUM_LIQUIDITY);
        (uint256 reserveWeth, uint256 reserveToken) = pair.getReserves();

        assertEq(reserveWeth, initParams.virtualEth);
        assertEq(reserveToken, initParams.initialTokenMatch);

        GoatTypes.InitialLPInfo memory initialLPInfo = pair.getInitialLPInfo();
    }

    function test_mintingFailsWithFOT() public {
        GoatTypes.InitParams memory initParams;
        initParams.virtualEth = 10e18;
        initParams.initialEth = 0;
        initParams.initialTokenMatch = 1000e18;
        initParams.bootstrapEth = 10e18;

        // Minting within this internal func reverts!!
        _mintInitialLiquidity(initParams, users.lp);
    }
```

</details>
   
## Tool used
Manual Review

## Recommendation
The code should be refactored to account for the fee deducted whenever FOT tokens like `PAXG` or `STA` are transferred.