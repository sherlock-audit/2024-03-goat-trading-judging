Interesting Eggshell Kookaburra

high

# Loss of funds for liquidity providers

## Summary
The issue lies within the GoatV1Pair contract, specifically in the way the project handles casting. Let's examine the code of the function` _update()`:
```solidity
    function _update(uint256 balanceEth, uint256 balanceToken, bool deductFees) internal {
        // Update token reserves and other necessary data
        if (deductFees) {
            _reserveEth = uint112(balanceEth - (_pendingLiquidityFees + _pendingProtocolFees));
        } else {
            _reserveEth = uint112(balanceEth);
        }
        _reserveToken = uint112(balanceToken);
    }
```
This function updates the reserves of ETH and the token based on the balances existing in the pool. However, currently, the project is casting from `uint256 balanceToken` to `uint112(balanceToken)`. _reserveToken represents the balance of the pair and how much it holds of a specific token. Unsafe casting means that if the value of balanceToken is larger than uint112, this will silently overflow and cause `_reserveToken `to be 0. This function is also used in every function such as `swap`, `burn`, and` mint`. Having 0 as _reserveToken when there is a balance will result in a massive DOS.

## Vulnerability Detail
The vulnerability exists  when users creates pool using high total supply tokens with values more than max uint112, since on the contest details the project is accepting any issues from ERC20 tokens because they accept all kind of tokens. 

Quote from contest page:
>Which ERC20 tokens do you expect will interact with the smart contracts?

>Any ERC20 tokens should be able to be used. 

If someone creates an LP using high/big total supply tokens such as memecoins for example, the first person to add liquidity will  lose all of the liquidity added he also cant remove liquidity . 

Lets break down this step by step:
1) User will create any ERC20 token with high total supply
2) User will create pair and addliquidity  using all the total supply of the token. 
3) The balance of the pair will be more than uint112 because the _update accepts uint256 as balance of token and cast it to a lower uint.
4) When this line is executed:` _reserveToken = uint112(balanceToken)`, the value of _reserveToken becomes 0.
5) Now, for the user who added liquidity, nothing appears to have gone wrong because this overflow reverts silently. When users attempt to swap, their transaction will revert with "InsufficientAmountOut" because the swap function calls `_getActualReserves` to get the `initialReserveToken`, which is now 0 because `initialReserveToken = _reserveToken`. In the swap function, we check if the amount of this operation is greater than the reserve of either ETH or the token, reverting with "InsufficientAmountOut."
```solidity
   function swap(uint256 amountTokenOut, uint256 amountWethOut, address to) external nonReentrant {
        ...
        ..
        (swapVars.initialReserveEth, swapVars.initialReserveToken) = _getActualReserves();
        if (amountTokenOut > swapVars.initialReserveToken || amountWethOut > swapVars.initialReserveEth) {
            revert GoatErrors.InsufficientAmountOut();
        }
        }
```
6) Since the swap is now reverted and no one can trade this token, if liquidity providers want to remove liquidity, this transaction will also fail with "InsufficientLiquidityBurned." This is because the burn function calculates the amountToken based on _reserveToken, which is 0 in this case.
```solidity
   function burn(address to) external returns (uint256 amountWeth, uint256 amountToken) {
        uint256 liquidity = balanceOf(address(this));
    ...
        uint256 totalSupply_ = totalSupply();
        amountWeth = (liquidity * _reserveEth) / totalSupply_;
        amountToken = (liquidity * _reserveToken) / totalSupply_;
        if (amountWeth == 0 || amountToken == 0) {
            revert GoatErrors.InsufficientLiquidityBurned();
        }
    ...
    }
```

<details>

<summary>POC Details</summary>

### Token Code

Add this code in  MockERC20.sol.
```solidity
contract TokenBigSupp is ERC20 {
    constructor() ERC20("BigSup", "BGS") {
        _mint(msg.sender, 100000000000000* 10 ** 18);
    }

    function mint(address to, uint256 amount) public {
        _mint(to, amount);
    }
}
```

### SetUp

We need to first deploy the token in the setUp() function in BaseTest.t.sol.
```solidity
tokenBigSupp = new TokenBigSupp();
```
Then in the same file we need to add two function so that our POC work

1)This addLiquidityParamsForPoc function is for setuping all the initial param for the liquidity to handle both cases before presale and after presale.
```solidity
    function addLiquidityParamsForPoc(bool initial, bool sendInitWeth) public returns (AddLiquidityParams memory) {
        weth.deposit{value: 100e18}();
        if (initial) {
            /* ------------------------------- SET PARAMS ------------------------------- */
            addLiqParams.token = address(tokenBigSupp);
            addLiqParams.tokenDesired = 0;
            addLiqParams.wethDesired = 0;
            addLiqParams.tokenMin = 0;
            addLiqParams.wethMin = 0;
            addLiqParams.to = address(this);
            addLiqParams.deadline = block.timestamp + 1000;

            addLiqParams.initParams = GoatTypes.InitParams(10e18, 10e18, sendInitWeth ? 5e18 : 0, 100000000000000e18); 
        } else {
            addLiqParams.token = address(tokenBigSupp);
            addLiqParams.tokenDesired = 1000000000e18; 
            addLiqParams.wethDesired = 1e18;
            addLiqParams.tokenMin = 0;
            addLiqParams.wethMin = 0;
            addLiqParams.to = address(this);
            addLiqParams.deadline = block.timestamp + 1000;

            addLiqParams.initParams = GoatTypes.InitParams(0, 0, 0, 0);
        }
        return addLiqParams;
    }
```
2)This second function will be called the first time to add liquidity into the pool.
```solidity
  
   function _addLiquidityAndConvertToAmmForPoc()
    internal
    returns (uint256 tokenAmtUsed, uint256 wethAmtUsed, uint256 liquidity, uint256 actualTokenAmountToSend)
{
    addLiquidityParamsForPoc(true, true);
    addLiqParams.initParams.initialEth = 10e18; // set all weth
    actualTokenAmountToSend = router.getActualBootstrapTokenAmount(
        addLiqParams.initParams.virtualEth,
        addLiqParams.initParams.bootstrapEth,
        addLiqParams.initParams.initialEth,
        addLiqParams.initParams.initialTokenMatch
    );


    tokenBigSupp.approve(address(router), 1000000000000000000000e18);

    weth.approve(address(router), addLiqParams.initParams.initialEth);
    (tokenAmtUsed, wethAmtUsed, liquidity) = router.addLiquidity(
        addLiqParams.token,
        addLiqParams.tokenDesired,
        addLiqParams.wethDesired,
        addLiqParams.tokenMin,
        addLiqParams.wethMin,
        addLiqParams.to,
        addLiqParams.deadline,
        addLiqParams.initParams
    );
}
```


### Code

Add this function in GoatV1Router.t.sol so that we can emitate how a normal user will add liquidity and not directly interact with the pair. The function below will revert with InsufficientLiquidityBurned.
```solidity
  function testRemoveLiquidityUsingHighTotalSupTokens() public {
        _addLiquidityAndConvertToAmmForPoc();
        GoatV1Pair pair = GoatV1Pair(factory.getPool(addLiqParams.token));
        uint256 lpTotalSupply = pair.totalSupply();
        //AT THIS POINT PRESALE IS ENDED
        addLiqParams = addLiquidityParamsForPoc(false, false); // new params
        // mint tokens to lp
        tokenBigSupp.mint(lp_1, 1000000000e18);
        weth.transfer(lp_1, 1e18);
        // Lp provides liqudity
        vm.startPrank(lp_1);
        tokenBigSupp.approve(address(router), 1000000000e18);
        weth.approve(address(router), 1e18);
        addLiqParams.to = lp_1; // change to lp
        // (uint256 reserveEth, uint256 reserveToken) = pair.getReserves(); // get reserves before adding liquidity to check for Lp minted later
        (uint256 tokenAmtUsed, uint256 wethAmtUsed, uint256 liquidity) = router.addLiquidity(
            addLiqParams.token,
            addLiqParams.tokenDesired,
            addLiqParams.wethDesired,
            addLiqParams.tokenMin,
            addLiqParams.wethMin,
            addLiqParams.to,
            addLiqParams.deadline,
            addLiqParams.initParams
        );
        vm.warp(block.timestamp + 2 days);
        pair.approve(address(router),100);
        router.removeLiquidity(address(tokenBigSupp), 100, 0, 0, lp_1, block.timestamp);
        vm.stopPrank();    
    }
  
```

</details>

## Impact
This unsafe casting will result in a massive DOS for all liquidity providers , they wont be able to remove liquidity and get their original funds back once this issue happens.
## Code Snippet

https://github.com/sherlock-audit/2024-03-goat-trading/blob/main/goat-trading/contracts/exchange/GoatV1Pair.sol#L633-L641

## Tool used

Manual Review

## Recommendation
Consider verifying that the values are within the acceptable range before casting, or use openzepplin safeCasting library.