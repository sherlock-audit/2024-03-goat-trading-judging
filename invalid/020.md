Shiny Eggshell Meerkat

medium

# Divide before multiply in `getWethAmountOutPresale` causes loss of funds

## Summary
Function `getWethAmountOutPresale` of `GoatLibrary` performs multiplication on the result of a division which causes loss of precision and funds.

## Vulnerability Detail
```solidity
File: contracts/library/GoatLibrary.sol

/// @audit ******************* Issue Detail *******************
GoatLibrary.getWethAmountOutPresale(uint256,uint256,uint256,uint256,uint256) (contracts/library/GoatLibrary.sol#143-161) performs a multiplication on the result of a division:
	- actualAmountWETHOut = numerator / denominator (contracts/library/GoatLibrary.sol#158)
	- amountWethOut = (actualAmountWETHOut * 9901) / 10000 (contracts/library/GoatLibrary.sol#160)

/// @audit ************** Possible Issue Line(s) **************
	L#158,  L#160,  

/// @audit ****************** Affected Code *******************
 143:     function getWethAmountOutPresale(
 144:         uint256 amountTokenIn,
 145:         uint256 reserveEth,
 146:         uint256 reserveToken,
 147:         uint256 virtualEth,
 148:         uint256 virtualToken
 149:     ) internal pure returns (uint256 amountWethOut) {
 150:         if (amountTokenIn == 0) revert GoatErrors.InsufficientInputAmount();
 151:         if (reserveEth == 0 || reserveToken == 0) revert GoatErrors.InsufficientLiquidity();
 152:         amountTokenIn = amountTokenIn * 10000;
 153:         uint256 numerator;
 154:         uint256 denominator;
 155:         uint256 actualAmountWETHOut;
 156:         numerator = amountTokenIn * (virtualEth + reserveEth);
 157:         denominator = (virtualToken + reserveToken) * 10000 + amountTokenIn;
 158:         actualAmountWETHOut = numerator / denominator;
 159:         // 0.99% fee on WETH
 160:         amountWethOut = (actualAmountWETHOut * 9901) / 10000;
 161:     }
```

## Impact
the `amountWethOut` value computed by the function `getWethAmountOutPresale` will be lesser than the actual value.
Following Foundry Fuzzing test identifies the issue

**PoC Code**
```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.13;

contract GoatLibrary {
    
    function getWethAmountOutPresale(uint256 amountTokenIn,uint256 reserveEth,uint256 reserveToken,uint256 virtualEth,uint256 virtualToken) public pure returns (uint256 amountWethOut) {
        if (amountTokenIn == 0) revert ("GoatErrors.InsufficientInputAmount()");
        if (reserveEth == 0 || reserveToken == 0)
            revert ("GoatErrors.InsufficientLiquidity()");
        amountTokenIn = amountTokenIn * 10000;
        uint256 numerator;
        uint256 denominator;
        uint256 actualAmountWETHOut;
        numerator = amountTokenIn * (virtualEth + reserveEth);
        denominator = (virtualToken + reserveToken) * 10000 + amountTokenIn;
        actualAmountWETHOut = numerator / denominator;
        // 0.99% fee on WETH
        amountWethOut = (actualAmountWETHOut * 9901) / 10000;
    }

    //Optimized Code - Division before multiplication removed
    function getWethAmountOutPresale_Optimize(uint256 amountTokenIn,uint256 reserveEth,uint256 reserveToken,uint256 virtualEth,uint256 virtualToken) public pure returns (uint256 amountWethOut) {
        if (amountTokenIn == 0) revert ("GoatErrors.InsufficientInputAmount()");
        if (reserveEth == 0 || reserveToken == 0)
            revert ("GoatErrors.InsufficientLiquidity()");
        amountTokenIn = amountTokenIn * 10000;
        uint256 numerator;
        uint256 denominator;
        uint256 actualAmountWETHOut;
        numerator = amountTokenIn * (virtualEth + reserveEth);
        denominator = (virtualToken + reserveToken) * 10000 + amountTokenIn;
        actualAmountWETHOut = numerator / denominator;
        // 0.99% fee on WETH
        //amountWethOut = (actualAmountWETHOut * 9901) / 10000;
        amountWethOut = (numerator * 9901) / 10000 / denominator;       //division before multiplication removed
    }
}
```
**Fuzz Testing Code**
```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.13;

import {Test, console} from "forge-std/Test.sol";
import {GoatLibrary} from "../src/GoatLibrary.sol";
import "forge-std/console.sol";

contract CounterTest is Test {
    GoatLibrary public lib;

    function setUp() public {
        lib = new GoatLibrary();
    }

    /// forge-config: default.fuzz.runs = 1000
    function testFuzz_getWethAmountOutPresale(
        uint256 amountTokenIn,
        uint256 reserveEth,
        uint256 reserveToken,
        uint256 virtualEth,
        uint256 virtualToken
    ) public view {
        uint256 low = 1 wei;
        uint256 high = 10000 ether;
        vm.assume(low <= amountTokenIn && amountTokenIn < high);
        vm.assume(low <= reserveEth && reserveEth < high);
        vm.assume(low <= reserveToken && reserveToken < high);
        vm.assume(low <= virtualEth && virtualEth < high);
        vm.assume(low <= virtualToken && virtualToken < high);

        uint256 amountWethOut = lib.getWethAmountOutPresale(amountTokenIn, reserveEth, reserveToken, virtualEth, virtualToken);
        uint256 amountWethOut_Optimize = lib.getWethAmountOutPresale_Optimize(amountTokenIn, reserveEth, reserveToken, virtualEth, virtualToken);(amountTokenIn, reserveEth, reserveToken, virtualEth, virtualToken);

        assertEq(amountWethOut, amountWethOut_Optimize);
    }
}
```

**Fuzz Test Output**
```console
└─$ forge test
[⠒] Compiling...
[⠆] Compiling 2 files with 0.8.24
[⠰] Solc 0.8.24 finished in 1.14s
Compiler run successful!

Ran 1 test for test/GoatLibrary.t.sol:CounterTest
[FAIL. Reason: assertion failed: 656 != 657; 
counterexample: calldata=0x24848f040000000000000000000000000000000000000000000000000000000000000d72000000000000000000000000000000000000000000000000000000000000017000000000000000000000000000000000000000000000000000000000000003e4000000000000000000000000000000000000000000000000000000000000048d0000000000000000000000000000000000000000000000000000000000000db9 
args=[3442, 368, 996, 1165, 3513]] 
testFuzz_getWethAmountOutPresale(uint256,uint256,uint256,uint256,uint256) (runs: 0, μ: 0, ~: 0)
Suite result: FAILED. 0 passed; 1 failed; 0 skipped; finished in 1.03ms (730.00µs CPU time)
```

## Code Snippet
https://github.com/sherlock-audit/2024-03-goat-trading/blob/main/goat-trading/contracts/library/GoatLibrary.sol#L143-L161

## Tool used
Manual Aided Review and Fuzz Testing

## Recommendation
remove division before multiplication
```solidity
        actualAmountWETHOut = numerator / denominator;
        // 0.99% fee on WETH
        //amountWethOut = (actualAmountWETHOut * 9901) / 10000;
        amountWethOut = (numerator * 9901) / 10000 / denominator;       //division before multiplication removed
```