Shiny Eggshell Meerkat

medium

# Divide before multiply in `_handleFees` computes wrong fee

## Summary
Function `_handleFees` of `GoatV1Pair` performs multiplication on the result of a division which result in wrong computation of fee and loss of funds.

## Vulnerability Detail
```solidity
File: contracts/exchange/GoatV1Pair.sol

/// @audit ******************* Issue Detail *******************
GoatV1Pair._handleFees(uint256,uint256,bool) (contracts/exchange/GoatV1Pair.sol#693-727) performs a multiplication on the result of a division:
	- feesLp = (feesCollected * 40) / 100 (contracts/exchange/GoatV1Pair.sol#706)
	- feesPerTokenStored += uint184((feesLp * 1e18) / totalSupply()) (contracts/exchange/GoatV1Pair.sol#714)

/// @audit ************** Possible Issue Line(s) **************
	L#706,  L#714,  

/// @audit ****************** Affected Code *******************
 706:         feesLp = (feesCollected * 40) / 100;
 714:             feesPerTokenStored += uint184((feesLp * 1e18) / totalSupply());
```

## Impact
the `feesPerTokenStored` value computed by the function `_handleFees` will be lesser than the actual value.
Following Foundry Fuzzing test identifies the issue

**PoC Code**
```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.13;

contract GoatV1Pair {

    uint184 public feesPerTokenStored;

    function _handleFees(uint256 amountWethIn, uint256 amountWethOut, bool isPresale)
        public
        returns (uint256 feesCollected, uint256 feesLp)
    {
        if (amountWethIn != 0) {
            feesCollected = (amountWethIn * 99) / 10000;
        } else {
            feesCollected = (amountWethOut * 10000) / 9901 - amountWethOut;
        }
        // lp fess is fixed 40% of the fees collected of total 99 bps
        feesLp = (feesCollected * 40) / 100;

        //uint256 pendingProtocolFees = _pendingProtocolFees;

        if (!isPresale) {
            //_pendingLiquidityFees += uint112(feesLp);
            feesPerTokenStored += uint184((feesLp * 1e18) / totalSupply());
        }

        /*
        rest of the code
        */
    }

    function totalSupply() public pure returns (uint256) {
        return 1_000_000_000_000;
    }
}

contract GoatV1Pair_Optimized {

    uint184 public feesPerTokenStored;

    function _handleFees_Optimized(uint256 amountWethIn, uint256 amountWethOut, bool isPresale)
        public
        returns (uint256 feesCollected, uint256 feesLp)
    {
        if (amountWethIn != 0) {
            feesCollected = (amountWethIn * 99) / 10000;
        } else {
            feesCollected = (amountWethOut * 10000) / 9901 - amountWethOut;
        }
        // lp fess is fixed 40% of the fees collected of total 99 bps
        feesLp = (feesCollected * 40) / 100;

        //uint256 pendingProtocolFees = _pendingProtocolFees;

        if (!isPresale) {
            //_pendingLiquidityFees += uint112(feesLp);
            feesPerTokenStored += uint184((feesCollected * 40 * 1e18) / totalSupply() / 100);
        }

        /*
        rest of the code
        */
    }

    function totalSupply() public pure returns (uint256) {
        return 1_000_000_000_000;
    }
}
```

**Fuzz Testing Code**
```solidity

// Import testing framework
import {Test, console} from "forge-std/Test.sol";

// Import your contracts
import "../src/GoatV1Pair.sol";
import "../src/GoatV1Pair_Optimized.sol";

contract GoatV1PairTest is Test {

    // Define contract instances with storage
    GoatV1Pair public goatV1Pair;
    GoatV1Pair_Optimized public goatV1PairOptimized;

    function setUp() public {
        goatV1Pair = new GoatV1Pair();
        goatV1PairOptimized = new GoatV1Pair_Optimized(); 
    }

    /// forge-config: default.fuzz.runs = 1000
    function testFuzz_handleFees(
        uint256 amountWethIn,
        uint256 amountWethOut) public {

        vm.assume(0 wei <= amountWethIn && amountWethIn < 1000 ether);
        vm.assume(0 wei <= amountWethOut && amountWethOut < 1000 ether);

        // Call functions with fuzzed inputs and store state
        goatV1Pair._handleFees(amountWethIn, amountWethOut, false);
        goatV1PairOptimized._handleFees_Optimized(amountWethIn, amountWethOut, false);

        // Access stored state for assertions
        uint256 feesPerTokenStoredV1 = goatV1Pair.feesPerTokenStored();
        uint256 feesPerTokenStoredV1Optimized = goatV1PairOptimized.feesPerTokenStored();

        assertEq(feesPerTokenStoredV1Optimized, feesPerTokenStoredV1);
    }
}
```

**Fuzz Test Output**
```console
└─$ forge test -vv --mt testFuzz_handleFees
[⠒] Compiling...
No files changed, compilation skipped

Ran 1 test for test/GoatV1Pair.t.sol:GoatV1PairTest
[FAIL. Reason: assertion failed: 13600000 != 13000000; 
counterexample: calldata=0x370734900000000000000000000000000000000000000000000000000000000000000da90000000000000000000000000000000000000000000000000000000000000216 
args=[3497, 534]] 
testFuzz_handleFees(uint256,uint256) (runs: 0, μ: 0, ~: 0)
Suite result: FAILED. 0 passed; 1 failed; 0 skipped; finished in 3.47ms (2.60ms CPU time)

Ran 1 test suite in 9.42ms (3.47ms CPU time): 0 tests passed, 1 failed, 0 skipped (1 total tests)
```

**Unit Test**
following unit test was written to calculate difference in `feesPerTokenStored` value over 1000 calls to `_handleFees`

```solidity
// Import testing framework
import {Test, console} from "forge-std/Test.sol";

// Import your contracts
import "../src/GoatV1Pair.sol";
import "../src/GoatV1Pair_Optimized.sol";

contract PairComparisonTest is Test {
    // Define contract instances with storage
    GoatV1Pair public goatV1Pair;
    GoatV1Pair_Optimized public goatV1PairOptimized;

    constructor(){
        goatV1Pair = new GoatV1Pair();
        goatV1PairOptimized = new GoatV1Pair_Optimized();
    }

    function setUp() public {
        
    }

    /// @notice Fuzz test to compare feesPerTokenStored after multiple calls
    function test_FeesPerTokenStoredDifference() public {
        for (int i = 0; i < 1000; i++) {
            uint256 amountWethIn = uint64(uint256(keccak256(abi.encodePacked(block.timestamp, i))));
            uint256 amountWethOut = uint64(uint256(keccak256(abi.encodePacked(amountWethIn, block.timestamp, i))));

            // Call functions with fuzzed inputs and store state
            goatV1Pair._handleFees(amountWethIn, amountWethOut, false);
            goatV1PairOptimized._handleFees_Optimized(amountWethIn,amountWethOut,false);

            // Handling the if-else branch where amountWethIn == 0
            goatV1Pair._handleFees(0, amountWethOut, false);
            goatV1PairOptimized._handleFees_Optimized(0,amountWethOut,false);
        }
        
        uint256 feesPerTokenStoredV1 = goatV1Pair.feesPerTokenStored();
        uint256 feesPerTokenStoredV1Optimized = goatV1PairOptimized.feesPerTokenStored();

        console.log("Difference: %s, %s", feesPerTokenStoredV1Optimized, feesPerTokenStoredV1);
        console.log("Difference: %s", feesPerTokenStoredV1Optimized - feesPerTokenStoredV1);
    }
}
```
Output of 1000 iterations test is
```console
└─$ forge test -vv --mt test_FeesPerTokenStoredDifference
[⠒] Compiling...
[⠰] Compiling 1 files with 0.8.24
[⠔] Solc 0.8.24 finished in 1.21s
Compiler run successful with warnings:

Ran 1 test for test/PairComparison.t.sol:PairComparisonTest
[PASS] test_FeesPerTokenStoredDifference() (gas: 9365561)
Logs:
  Values: 73769309073434194543200000, 73769309073434193742000000
  Difference: 801200000
```


## Code Snippet
https://github.com/sherlock-audit/2024-03-goat-trading/blob/main/goat-trading/contracts/exchange/GoatV1Pair.sol#L693-L715

## Tool used
Manual Aided Review and Fuzz Testing

## Recommendation
remove division before multiplication
```solidity
714:        feesPerTokenStored += uint184((feesCollected * 40 * 1e18) / totalSupply() / 100);           //division before multiplication removed
```